// ---------------------------------------------
// Friendship
// ---------------------------------------------
// Original talk #3 trigger
IF
    RealGlobalTimerExpired("YoshimoTalksPC3","GLOBAL")
    InParty(Myself)
    Global("I#Yoshimo_BY5","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("I#Yoshimo_BY5","GLOBAL",2)
    RealSetGlobalTimer("I#YoshimoFriendTalksTimer","GLOBAL",3600)
    SetGlobal("I#YoshimoFriendshipActive","GLOBAL",2)
    SetGlobal("I#YoshimoFriendStart","GLOBAL",1)
END

IF
    RealGlobalTimerExpired("YoshimoTalksPC3","GLOBAL")
    InParty(Myself)
    Global("I#Yoshimo_BY5","GLOBAL",2)
    Global("BYoshimo5","LOCALS",0)
THEN
  RESPONSE #100
    Interact(Player1)
END


// Triggering friendship talks
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("I#YoshimoFriendTalksTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    OR(4)
      Global("I#YoshimoFriendTalks","GLOBAL",3)
      Global("I#YoshimoFriendTalks","GLOBAL",5)
      Global("I#YoshimoFriendTalks","GLOBAL",7)
      Global("I#YoshimoFriendTalks","GLOBAL",11)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    StartDialogueNoSet(Player1)
END

IF
    InParty(Myself)
    See(Player1)
    RealGlobalTimerExpired("I#YoshimoFriendTalksTimer","GLOBAL")
    OR(4)
      Global("I#YoshimoFriendTalks","GLOBAL",2)
      Global("I#YoshimoFriendTalks","GLOBAL",4)
      Global("I#YoshimoFriendTalks","GLOBAL",6)
      Global("I#YoshimoFriendTalks","GLOBAL",10)
THEN
  RESPONSE #100
    IncrementGlobal("I#YoshimoFriendTalks","GLOBAL",1)
END


// First talk triggers in "seedy" areas of Athkatla
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("I#YoshimoFriendTalksTimer","GLOBAL")
    Global("I#YoshimoFriendStart","GLOBAL",1)
    OR(6)
      AreaCheck("AR0300") // Docks District
      AreaCheck("AR0400") // Slums District
      AreaCheck("AR0500") // Bridge District
      AreaCheck("AR0021") // City Gates - Crooked Crane 1st floor
      AreaCheck("AR0313") // Docks - Sea's Bounty 1st floor
      AreaCheck("AR0406") // Slums - Copper Coronet
THEN
  RESPONSE #100
    SetGlobal("I#YoshimoFriendTalks","GLOBAL",1)
    IncrementGlobal("I#YoshimoFriendStart","GLOBAL",1)
END    

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    Global("I#YoshimoFriendTalks","GLOBAL",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    StartDialogueNoSet(Player1)
END



// Fifth talk triggers in chapter 3
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("I#YoshimoFriendTalksTimer","GLOBAL")
    Global("I#YoshimoFriendTalks","GLOBAL",8)
    Global("WorkingForAran","GLOBAL",1)
    GlobalGT("chapter","GLOBAL",%bg2_chapter_2%)
THEN
  RESPONSE #100
    IncrementGlobal("I#YoshimoFriendTalks","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    Global("I#YoshimoFriendTalks","GLOBAL",9)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    StartDialogueNoSet(Player1)
END



// Seventh talk triggers after Bodhi encounter
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("I#YoshimoFriendTalksTimer","GLOBAL")
    Global("I#YoshimoFriendTalks","GLOBAL",12)
    Global("LassalVampires","GLOBAL",3)
THEN
  RESPONSE #100
    IncrementGlobal("I#YoshimoFriendTalks","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    Global("I#YoshimoFriendTalks","GLOBAL",13)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    StartDialogueNoSet(Player1)
END



// Eighth talk triggers in Brynnlaw

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !Global("WorkingForAran","GLOBAL",1)
    Global("I#YoshimoFriendTalks","GLOBAL",8)
    OR(3)
      AreaCheck("AR1600")
      AreaCheck("AR1602")
      AreaCheck("AR1603")
 THEN
  RESPONSE #100
    SetGlobal("I#YoshimoFriendTalks","GLOBAL",15)
    RealSetGlobalTimer("I#YoshimoFriendTalksTimer","GLOBAL",600)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    Global("I#YoshimoFriendTalks","GLOBAL",14)
    OR(3)
      AreaCheck("AR1600")
      AreaCheck("AR1602")
      AreaCheck("AR1603")
THEN
  RESPONSE #100
    IncrementGlobal("I#YoshimoFriendTalks","GLOBAL",1)
    RealSetGlobalTimer("I#YoshimoFriendTalksTimer","GLOBAL",600)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    Global("I#YoshimoFriendTalks","GLOBAL",15)
    RealGlobalTimerExpired("I#YoshimoFriendTalksTimer","GLOBAL")
THEN
  RESPONSE #100
    MoveToObject(Player1)
    StartDialogueNoSet(Player1)
END



// Ninth talk triggers in Spellhold
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    Global("I#YoshimoFriendTalks","GLOBAL",16)
    AreaCheck("AR1515")
THEN
  RESPONSE #100
    IncrementGlobal("I#YoshimoFriendTalks","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    Global("I#YoshimoFriendTalks","GLOBAL",17)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    StartDialogueNoSet(Player1)
END
